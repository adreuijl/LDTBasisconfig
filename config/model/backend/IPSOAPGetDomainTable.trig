GRAPH config:Theatre {

  config:GetDomainTable a elmo:InformationProduct;
    elmo:resultType elmo-rt:Tuple;
    elmo:backend config:LocalBackend;
    elmo:requiredParameter config:DomainTableParameter;
    elmo:optionalParameter config:CheckDateParameter;
    elmo:query """
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX prov: <http://www.w3.org/ns/prov#>
      PREFIX sh: <http://www.w3.org/ns/shacl#>
      PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
      PREFIX dct: <http://purl.org/dc/terms/>
      PREFIX dcmiperiod: <http://dublincore.org/documents/2006/04/10/dcmi-period/>
      PREFIX dcat: <http://www.w3.org/ns/dcat#>
      PREFIX foaf: <http://xmlns.com/foaf/0.1/>
      SELECT ?count ?domaintablename ?id ?begindate ?enddate ?isnull ?property ?datatype ?value
      WHERE {
        {
          SELECT (count(*) as ?count)
          WHERE {
            GRAPH <http://standaard.aquo.nl/data/domeintabelmetadata> {
              ?domaintable rdfs:label ?domaintablename.
              ?domaintable dcat:landingPage ?domaintablegraph.
            }
            GRAPH ?domaintablegraph {
              ?temporal dcmiperiod:start ?start
              OPTIONAL {?temporal dcmiperiod:end ?endr}
              BIND (if(bound(?endr),?endr,"2999-12-31") as ?end)
              FILTER (str(?start) <= ?checkdate && ?checkdate < str(?end))
            }
          }
        }
        {
        SELECT ?domaintablename
               ?id
               ?begindate
               (if(bound(?enddater),?enddater,"2999-12-31") as ?enddate)
               ?isnull
               ?property
               (concat("http://rws.services.nl/DomainTableWS/Contracts/2010/10",if(bound(?dtype),if(?dtype=xsd:int,"Integer","String"),"String"),"Field") as ?datatype)
  #             (concat("ns1:",if(bound(?dtype),if(?dtype=xsd:int,"Integer","String"),"String"),"Field") as ?datatype)
               ?value
               ?nr
        WHERE {
          GRAPH <http://standaard.aquo.nl/data/domeintabelmetadata> {
            ?domaintable rdfs:label ?domaintablename.
            ?domaintable dcat:landingPage ?domaintablegraph.
          }
          OPTIONAL {
            {
              # Normale waarde
              BIND (false as ?isnull)
              GRAPH ?domaintablegraph {
                ?id a ?type.
                ?id foaf:isPrimaryTopicOf/dct:temporal/dcmiperiod:start ?begindate.
                OPTIONAL {?id foaf:isPrimaryTopicOf/dct:temporal/dcmiperiod:end ?enddater}
                ?id ?prop ?value
              }
              GRAPH <http://standaard.aquo.nl/def/domeintabel> {
                ?shape sh:targetClass ?type.
                ?shape sh:property ?meta.
                ?meta sh:path ?prop.
                ?meta rdfs:label ?property.
                ?meta sh:order ?nr.
                ?meta sh:datatype ?dtype.
              }
            }
            UNION
            {
              # Resource (groep)
              BIND (false as ?isnull)
              GRAPH ?domaintablegraph {
                ?id a ?type.
                ?id foaf:isPrimaryTopicOf/dct:temporal/dcmiperiod:start ?begindate.
                OPTIONAL {?id foaf:isPrimaryTopicOf/dct:temporal/dcmiperiod:end ?enddater}
                ?id ?prop ?valueresource.
                ?valueresource rdfs:label ?value
              }
              GRAPH <http://standaard.aquo.nl/def/domeintabel> {
                ?shape sh:targetClass ?type.
                ?shape sh:property ?meta.
                ?meta sh:path ?prop.
                ?meta rdfs:label ?property.
                ?meta sh:order ?nr.
                ?meta sh:class ?valueclass.
              }
            }
            UNION
            {
              # Geen waarde, dan dit ook teruggeven (closed model)
              BIND (true as ?isnull)
              GRAPH ?domaintablegraph {
                ?id a ?type.
                ?id foaf:isPrimaryTopicOf/dct:temporal/dcmiperiod:start ?begindate.
                OPTIONAL {?id foaf:isPrimaryTopicOf/dct:temporal/dcmiperiod:end ?enddater}
              }
              GRAPH <http://standaard.aquo.nl/def/domeintabel> {
                ?shape sh:targetClass ?type.
                ?shape sh:property ?pshape.
                ?pshape sh:path ?prop.
                ?pshape rdfs:label ?property.
                ?pshape sh:order ?nr.
                OPTIONAL {?pshape sh:datatype ?dtype}.
              }
              FILTER NOT EXISTS {
                GRAPH ?domaintablegraph {
                  ?id ?prop ?value
                }
              }
            }
          }
        }
        }
        FILTER (str(?begindate) <= ?checkdate && ?checkdate < str(?enddate))
      }
      ORDER BY ?id ?nr
    """
  .

}
