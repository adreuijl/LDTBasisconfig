GRAPH config:Theatre { 
  
  config:ConceptDetailData a elmo:InformationProduct; 
    elmo:requiredParameter config:ConceptTabelParameter; 
    elmo:optionalParameter config:CategorieParameter;
    elmo:optionalParameter elmo:SubjectParameter;
    elmo:backend config:LocalBackend; 
    elmo:query """ 
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#> 
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#> 
      PREFIX dct: <http://purl.org/dc/terms/>
      PREFIX aquo: <http://data.aquo.nl/def/aquo#>
      
      CONSTRUCT { 
        ?subject ?p ?o;
          rdfs:label ?titellabel.
        ?o rdfs:label ?olabel.
        ?subject skos:semanticRelation ?domeintabel.
        ?domeintabel rdfs:label ?domeinlabel.
        ?subject dct:subject ?categorie.
        ?categorie rdfs:label ?categorie_label.
        ?subject aquo:englishLabel ?englishLabel.
        ?subject aquo:germanLabel ?germanLabel.
        ?subject aquo:flemmishLabel ?flemmishLabel.
      } 
      WHERE {
        GRAPH <http://data.aquo.nl/id/dataset/begrippen> { 
          ?subject ?p ?o; 
            rdfs:label ?slabel.
          OPTIONAL {?o rdfs:label ?olabel}
  		    OPTIONAL {?subject skos:altLabel ?englishLabel FILTER (lang(?englishLabel)="en")}
          OPTIONAL {?subject skos:altLabel ?germanLabel FILTER (lang(?germanLabel)="de")}
          OPTIONAL {?subject skos:altLabel ?flemmishLabel FILTER (lang(?flemmishLabel)="fl")}
          MINUS {?subject rdfs:label ?o}
        }
        OPTIONAL {
          GRAPH <http://data.aquo.nl/id/dataset/begrippen> {
            ?subject dct:subject ?categorie
          }
          GRAPH <http://data.aquo.nl/id/dataset/categorieen> {
            ?categorie rdfs:label ?categorie_label
          }
        }
        OPTIONAL {
          GRAPH <http://data.aquo.nl/id/dataset/begrippen> {
            ?subject skos:semanticRelation ?domeintabel
          }
          GRAPH <http://data.aquo.nl/id/dataset/domeintabel> {
            ?domeintabel rdfs:label ?domeinlabel
          }
        }
        
        BIND(CONCAT("Begrip: ", ?slabel) AS ?titellabel)
      } 
    """ 
  . 
  
}