GRAPH config:Theatre {

  config:DomeinWaardenData a elmo:InformationProduct;
      elmo:backend config:LocalBackend;
      elmo:optionalParameter elmo:SubjectParameter;
      elmo:resultType elmo-rt:Graph;
      elmo:query """

      PREFIX dct: <http://purl.org/dc/terms/>
      PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
      PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
      PREFIX aquo: <http://standaard.aquo.nl/def/aquo#>
      PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
      PREFIX owl:<http://www.w3.org/2002/07/owl#>
      PREFIX dcat: <http://www.w3.org/ns/dcat#>

      CONSTRUCT {
        ?subject ?p ?o;
          owl:sameAs ?begrip;
          dct:subject ?groep;
          dct:hasPart ?domeintabel;
          rdfs:label ?dlabel.
        ?begrip rdfs:label ?begrip_label.
        ?groep rdfs:label ?groep_label.
        ?domeintabel rdfs:label ?domeinlabel.
      }

      WHERE {
        GRAPH <http://standaard.aquo.nl/data/domeintabellen> {
          ?id dct:hasPart ?domeintabel.
          ?domeintabel rdfs:label ?domeinlabel.
        }

        GRAPH ?domeintabel {
          ?subject ?p ?o;
            rdf:type ?domeintype.
        }
        OPTIONAL {
          GRAPH ?domeintabel {
            ?subject aquo:Omschrijving ?subjectlabel;
              dct:subject ?groep.
            ?groep skos:prefLabel ?groep_label.
              }
        }
        OPTIONAL {
            GRAPH <http://standaard.aquo.nl/data/domeintabelmetadata> {
              ?dataset dcat:distribution ?dist.
              FILTER (?dist = ?subject)
            }
          }
        OPTIONAL { 
          GRAPH <http://standaard.aquo.nl/id/dataset/links> {
            ?subject owl:sameAs ?begrip
          }
          GRAPH  <http://standaard.aquo.nl/data/begrippen> {
            ?begrip skos:prefLabel ?begrip_label
          }
        }
        BIND(CONCAT("Domeinwaarde: ",?subjectlabel) AS ?dlabel)
      }

      """
  .
}
